plugins {
    id 'java-library' // Adds Java compiling, testing, bundling capabilities to this build project
}

repositories {
    jcenter() // Repository from which we retrieve dependency libraries
}

dependencies { // The dependencies for building/testing/running code in this build
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
}

test {
    useJUnitPlatform {
        // Default value for platform in gradle.properties, or override on command line: -Pplatform=some_value
        systemProperty "platform", project.getProperty("platform")
    }

    dependsOn 'cleanTest' // Always run tests, even when nothing changed.

    testLogging {
        // Log result for each test as they run
        events "passed", "skipped", "failed", "standardOut", "standardError"

        exceptionFormat = 'full' // For test failures, show assertion on why it failed instead of just the line number of failure
        showStandardStreams = true // Show stdout and stderr of tests on console
    }

    // Show summary of test run
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}
